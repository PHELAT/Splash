buildscript {
    ext.kotlin_version = '1.2.41'
    repositories {
        google()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:3.1.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.jacoco:org.jacoco.core:0.8.1'
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.0.0.RC7-2"
}

apply from: 'dependencies.gradle'

allprojects {

    repositories {
        google()
        jcenter()
    }

    configurations {
        all {
            resolutionStrategy {
                force "android.arch.lifecycle:runtime:$libraryVersions.architectureComponents"
                force "android.arch.lifecycle:livedata-core:$libraryVersions.architectureComponents"
                force "android.arch.lifecycle:viewmodel:$libraryVersions.architectureComponents"
            }
        }
    }

    apply plugin: 'jacoco'
    jacoco {
        toolVersion = '0.8.0'
    }
    tasks.withType(Test) {
        jacoco.includeNoLocationClasses = true
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

detekt {
    version = "1.0.0.RC7-2"
    defaultProfile {
        input = "$rootDir"
        config = "$rootDir/config/detekt/detektconfig.yml"
        output = "$rootDir/build/reports/detekt/"
        filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
    }
}